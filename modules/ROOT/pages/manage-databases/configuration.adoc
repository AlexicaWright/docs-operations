:description: This page describes how to manage multiple active databases.
[[manage-databases-configuration]]
= Administration and configuration

////
[source, cypher, role=test-setup]
----
CREATE DATABASE `movies`;
CREATE ALIAS `films` FOR DATABASE `movies`;
CREATE ALIAS `motion pictures` FOR DATABASE `movies`;
----
////

Neo4j supports the management of multiple databases within the same DBMS.
The metadata for these databases, including the associated security model, is maintained in a special database called the `system` database.
All multi-database administrative commands must be run against the `system` database.
These administrative commands are automatically routed to the `system` database when connected to the DBMS over Bolt.

[NOTE]
====
Administrative commands should not be used during a rolling upgrade.
For more information, see link:{neo4j-docs-base-uri}/upgrade-migration-guide/upgrade/upgrade-4.4/causal-cluster/[Upgrade and Migration Guide -> Upgrade a cluster].
====

[[manage-databases-rules-and-recommendations]]
== Rules and recommendations

Before using administrative commands, it is important to understand the difference between stopped databases, and dropped databases:

* Databases that are stopped with the `STOP` command are completely shutdown, and may be started again through the `START` command.
In a cluster, as long as a database is in a shutdown state, it can not be considered available to other members of the cluster.
It is not possible to do online backups against shutdown databases and they need to be taken into special consideration during disaster recovery, as they do not have a running Raft machine while shutdown.
* Dropped databases are completely removed and are not intended to be used again at all.

[[manage-databases-rules-and-recommendations-naming-rules]]
=== Naming rules

Naming rules for databases are as follows:

* Length must be between 3 and 63 characters.
* The first character of a name must be an ASCII alphabetic character.
* Subsequent characters must be ASCII alphabetic or numeric characters, dots or dashes; `[a..z][0..9].-`
* Names are case-insensitive and normalized to lowercase.
* Names that begin with an underscore and with the prefix `system` are reserved for internal use.

[NOTE]
====
Database names are subject to the link:{neo4j-docs-base-uri}/cypher-manual/{page-version}/syntax/naming[standard Cypher restrictions on valid identifiers].
In particular, the `-` (dash) and `.` (dot) characters are not legal in Cypher variables, and therefore names with dashes must be enclosed within back-ticks.
For example, `CREATE DATABASE ++`main-db`++`.
Database names are the only identifier for which dots don't need to be escaped.
For example, `main.db` is a valid database name.
====

It is possible to create an alias to refer to an existing database to avoid these restrictions.
For more information, see link:{neo4j-docs-base-uri}/cypher-manual/{page-version}/administration/aliases#alias-management-create-database-alias[Cypher Manual -> Creating database aliases].

For detailed information on Cypher administrative commands, see link:{neo4j-docs-base-uri}/cypher-manual/{page-version}/administration/access-control[Cypher Manual -> Access Control].

[[manage-databases-syntax]]
== Cypher syntax

The following Cypher commands are used on the `system` database to manage multiple databases:

.Database management command syntax
[options="header", width="100%", cols="1m,5a"]
|===
| Command | Syntax

| SHOW DATABASE
|
[source, syntax, role="noheader"]
----
SHOW { DATABASE[S] name \| DATABASE[S] \| DEFAULT DATABASE \| HOME DATABASE }
[WHERE expression]
----

[source, syntax, role="noheader"]
----
SHOW { DATABASE[S] name \| DATABASE[S] \| DEFAULT DATABASE \| HOME DATABASE }
YIELD { * \| field[, ...] } [ORDER BY field[, ...]] [SKIP n] [LIMIT n]
[WHERE expression]
[RETURN field[, ...] [ORDER BY field[, ...]] [SKIP n] [LIMIT n]]
----

| CREATE DATABASE
|
[source, syntax, role="noheader"]
----
CREATE DATABASE name [IF NOT EXISTS]
[TOPOLOGY n PRIMAR{Y\|IES} [m SECONDAR{Y\|IES}]]
[OPTIONS "{" option: value[, ...] "}"]
[WAIT [n [SEC[OND[S]]]]\|NOWAIT]
----

[source, syntax, role="noheader"]
----
CREATE OR REPLACE DATABASE name
[TOPOLOGY n PRIMAR{Y\|IES} [m SECONDAR{Y\|IES}]]
[OPTIONS "{" option: value[, ...] "}"]
[WAIT [n [SEC[OND[S]]]]\|NOWAIT]
----

| CREATE COMPOSITE DATABASE
|
[source, synatx, role="noheader"]
----
CREATE COMPOSITE DATABASE name [IF NOT EXISTS]
[OPTIONS "{" "}"]
[WAIT [n [SEC[OND[S]]]]\|NOWAIT]
----

[source, syntax, role="noheader"]
----
CREATE OR REPLACE COMPOSITE DATABASE name
[OPTIONS "{" "}"]
[WAIT [n [SEC[OND[S]]]]\|NOWAIT]
----

| ALTER DATABASE
|
[source, syntax, role="noheader"]
----
ALTER DATABASE name [IF EXISTS]
{
SET ACCESS {READ ONLY \| READ WRITE} \|
SET TOPOLOGY n PRIMAR{Y\|IES} [m SECONDAR{Y\|IES}]
}
[WAIT [n [SEC[OND[S]]]]\|NOWAIT]
----

| STOP DATABASE
|
[source, syntax, role="noheader"]
----
STOP DATABASE name [WAIT [n [SEC[OND[S]]]]\|NOWAIT]
----

| START DATABASE
|
[source, syntax, role="noheader"]
----
START DATABASE name [WAIT [n [SEC[OND[S]]]]\|NOWAIT]
----

| DROP DATABASE
|
[source, syntax, role="noheader"]
----
DROP [COMPOSITE] DATABASE name [IF EXISTS] [{DUMP\|DESTROY} [DATA]] [WAIT [n [SEC[OND[S]]]]\|NOWAIT]
----

|===

[[manage-databases-listing-databases]]
== Listing databases

There are four different commands for listing databases:

* Listing all databases.
* Listing a particular database.
* Listing the default database.
* Listing the home database.

These commands return the following columns:

.Listing databases output
[options="header", width="100%", cols="4m,6a,2m"]
|===
| Column | Description | Type

| name
| The name of the database. label:default-output[]
| STRING

| type
| The type of the database: `system`, `standard`, or `composite`. label:default-output[]
| STRING

| aliases
| The names of any aliases the database may have. label:default-output[]
| LIST OF STRING

| access
| The database access mode, either `read-write` or `read-only`. label:default-output[]
A database may be described as read-only when using `ALTER DATABASE ... SET ACCESS READ ONLY`.

| STRING

| databaseID
| The database unique ID.
| STRING

| serverID
| The server instance ID.
| STRING

| address
|
Instance address in a clustered DBMS.
The default for a standalone database is `neo4j://localhost:7687`. label:default-output[]
| STRING

| role
| The current role of the database (`primary`, `secondary`, `unknown`). label:default-output[]
| STRING

| writer
|`true` for the instance that accepts writes for this database (this instance is the leader for this database in a cluster or this is a standalone instance). label:default-output[]
| BOOLEAN

| requestedStatus
| The expected status of the database. label:default-output[]
| STRING

| currentStatus
| The actual status of the database. label:default-output[]
| STRING

| statusMessage
| A message explaining the status of the database, often explaining why it is not in the correct state. label:default-output[]
| STRING

| default
|
Show if this is the default database for the DBMS. label:default-output[]
Not returned by `SHOW HOME DATABASE` or `SHOW DEFAULT DATABASE`.

| BOOLEAN

| home
|
Shown if this is the home database for the current user. label:default-output[]
Not returned by `SHOW HOME DATABASE` or `SHOW DEFAULT DATABASE`.

| BOOLEAN

| `currentPrimariesCount`
| Number of primaries for this database reported as running currently.
It is the same as the number of rows where `role=primary` and `name=this database`.
| INTEGER

| `currentSecondariesCount`
| Number of secondaries for this database reported as running currently.
It is the same as the number of rows where `role=secondary` and `name=this database`.
| INTEGER

| `requestedPrimariesCount`
| The requested number of primaries for this database.
May be lower than current if the DBMS is currently reducing the number of copies of the database, or higher if it is currently increasing the number of copies.
| INTEGER

| `requestedSecondariesCount`
| The requested number of secondaries for this database.
May be lower than current if the DBMS is currently reducing the number of copies of the database, or higher if it is currently increasing the number of copies.
| INTEGER

| creationTime
| The date and time at which the database was created.
| DATETIME

| lastStartTime
| The date and time at which the database was last started.
| DATETIME

| lastStopTime
| The date and time at which the database was last stopped.
| DATETIME

| store
a|
Information about the storage engine and the store format.

The value is a string formatted as:

[source, syntax, role="noheader"]
----
{storage engine}-{store format}-{major version}.{minor version}
----
| STRING

| lastCommittedTxn
| The ID of the last transaction received.
| INTEGER

| replicationLag
|
Number of transactions the current database is behind compared to the database on the primary instance.
The lag is expressed in negative integers. In standalone environments, the value is always `0`.
| INTEGER

|constituents
|The names of any constituents the database may have. label:default-output[]
| LIST OF STRING

|===


.+SHOW DATABASES+
======

A summary of all available databases can be displayed using the command `SHOW DATABASES`.

.Query
[source, cypher]
----
SHOW DATABASES
----

.Result
[role="queryresult",options="header,footer",cols="13*<m"]
|===

| +name+ | +type+ | +aliases+ | +access+ | +address+ | +role+ | +writer+ | +requestedStatus+ | +currentStatus+ | +statusMessage+ | +default+ | +home+ | +constituents+
| +"movies"+ | +"standard"+ | +["films","motion pictures"]+ | +"read-write"+ | +"localhost:7687"+ | +"primary"+ | +true+ | +"online"+ | +"online"+ | +""+ | +false+ | +false+ | +[]+
| +"neo4j"+ | +"standard"+ | +[]+ | +"read-write"+ | +"localhost:7687"+ | +"primary"+ | +true+ | +"online"+ | +"online"+ | +""+ | +true+ | +true+ | +[]+
| +"system"+ | +"system"+ | +[]+ | +"read-write"+ | +"localhost:7687"+ | +"primary"+ | +true+ | +"online"+ | +"online"+ | +""+ | +false+ | +false+ | +[]+
13+d|Rows: 3

|===

The results of this command are filtered according to the `ACCESS` privileges of the user.
However, some privileges enable users to see additional databases regardless of their `ACCESS` privileges:

* Users with `CREATE/DROP/ALTER DATABASE` or `SET DATABASE ACCESS` privileges can see all standard databases.
* Users with `CREATE/DROP COMPOSITE DATABASE` or `COMPOSITE DATABASE MANAGEMENT` privileges can see all composite databases.
* Users with `DATABASE MANAGEMENT` privilege can see all databases.

If a user has not been granted `ACCESS` privilege to any databases nor any of the above special cases, the command can still be executed but will only return the `system` database, which is always visible.


[NOTE]
====
As of Neo4j 5.3, databases hosted on servers that are offline are also returned by the `SHOW DATABASES` command.
For such databases, the `address` column displays `NULL`, the `currentStatus` column displays `unknown`, and the `statusMessage` displays `Server is unavailable`.
====

======

.+SHOW DATABASES+
======

In this example, the detailed information for a particular database can be displayed using the command `SHOW DATABASE name YIELD *`.
When a `YIELD` clause is provided, the full set of columns is returned.

.Query
[source, cypher, role=test-result-skip]
----
SHOW DATABASE movies YIELD *
----

// Limited result set.
// The output has been capped.

.Result
[role="queryresult",options="header,footer",cols="7*<m"]
|===
| +name+ | +aliases+ | +access+ | +databaseID+ | +serverID+ | +address+ | +...+
| +"movies"+ | +["films","motion pictures"]+ | +"read-write"+ | +"367221F9021C00CEBFCA25C8E2101F1DCF45C7DB9BF7D7A0949B87745E760EDD"+ | +"adc0a7bc-d9a6-4cc8-b394-91635fbb1137"+ | +"localhost:7687"+ | +...+
7+d|Rows: 1
|===

======


.+SHOW DATABASES+
======

The number of databases can be seen using a `count()` aggregation with `YIELD` and `RETURN`.

.Query
[source, cypher]
----
SHOW DATABASES YIELD *
RETURN count(*) AS count
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +count+
| +3+
1+d|Rows: 1

|===

======


.+SHOW DEFAULT DATABASE+
======

The default database can be seen using the command `SHOW DEFAULT DATABASE`.

.Query
[source, cypher]
----
SHOW DEFAULT DATABASE
----

.Result
[role="queryresult",options="header,footer",cols="11*<m"]
|===

| +name+ | +type+ | +aliases+ | +access+ | +address+ | +role+ | +writer+ | +requestedStatus+ | +currentStatus+ | +statusMessage+ | +constituents+
| +"neo4j"+ | +"standard"+ | +[]+ | +"read-write"+ | +"localhost:7687"+ | +"primary"+ | +true+ | +"online"+ | +"online"+ | +""+ | +[]+
11+d|Rows: 1

|===

======


.+SHOW HOME DATABASE+
======

The home database for the current user can be seen using the command `SHOW HOME DATABASE`.

.Query
[source, cypher]
----
SHOW HOME DATABASE
----

.Result
[role="queryresult",options="header,footer",cols="11*<m"]
|===

| +name+ | +type+ | +aliases+ | +access+ | +address+ | +role+ | +writer+ | +requestedStatus+ | +currentStatus+ | +statusMessage+ | +constituents+
| +"neo4j"+ | +"standard"+ | +[]+ | +"read-write"+ | +"localhost:7687"+ | +"primary"+ | +true+ | +"online"+ | +"online"+ | +""+ | +[]+
11+d|Rows: 1

|===

======

.+SHOW DATABASES+
======

It is also possible to filter and sort the results by using `YIELD`, `ORDER BY`, and `WHERE`.

.Query
[source, cypher]
----
SHOW DATABASES YIELD name, currentStatus, requestedStatus
ORDER BY currentStatus
WHERE name CONTAINS 'o'
----

In this example:

* The number of columns returned has been reduced with the `YIELD` clause.
* The order of the returned columns has been changed.
* The results have been filtered to only show database names containing `'o'`.
* The results are ordered by the `currentStatus` column using `ORDER BY`.

It is also possible to use `SKIP` and `LIMIT` to paginate the results.


.Result
[role="queryresult",options="header,footer",cols="3*<m"]
|===

| +name+ | +currentStatus+ | +requestedStatus+
| +"movies"+ | +"online"+ | +"online"+
| +"neo4j"+ | +"online"+ | +"online"+
3+d|Rows: 2

|===


[NOTE]
====
Note that for failed databases, the `currentStatus` and `requestedStatus` are different.
This often implies an error, but **does not always**.
For example, a database may take a while to transition from `offline` to `online` due to performing recovery.
Or, during normal operation a database's `currentStatus` may be transiently different from its `requestedStatus` due to a necessary automatic process, such as one Neo4j instance copying store files from another.
The possible statuses are `initial`, `online`, `offline`, `store copying` and `unknown`.
====

======

[[manage-databases-creating-databases]]
== Creating databases

Databases can be created using `CREATE DATABASE`.

[NOTE]
====
Having dots (`.`) in the database names is not recommended.
This is due to the difficulty of determining if a dot is part of the database name or a delimiter for a database alias in a composite database.
====

.+CREATE DATABASE+
======

.Query
[source, cypher]
----
CREATE DATABASE customers
----

.Result
[source, result, role="noheader"]
----
System updates: 1
Rows: 0
----

======


.+SHOW DATABASES+
======

When a database has been created, it will show up in the listing provided by the command `SHOW DATABASES`.

.Query
[source, cypher]
----
SHOW DATABASES YIELD name
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +name+
| +"customers"+
| +"movies"+
| +"neo4j"+
| +"system"+
1+d|Rows: 7

|===

======

[[manage-databases-create-database-existing]]
=== handling new and existing databases

Appending `IF NOT EXISTS` to the `CREATE DATABASE` command ensures that no error is returned and nothing happens should the database already exist.
Adding `OR REPLACE` to the command will result in any existing database being deleted and a new one created.
These commands are optionally idempotent, with the default behavior to fail with an error if the database already exists.

These behavior flags apply to both standard and composite databases (e.g. a composite database may replace a standard one or another composite.)

.+CREATE DATABASE+
======

.Query
[source, cypher]
----
CREATE DATABASE customers IF NOT EXISTS
----

======


.+CREATE OR REPLACE DATABASE+
======

.Query
[source, cypher]
----
CREATE OR REPLACE DATABASE customers
----

This is equivalent to running `DROP DATABASE customers IF EXISTS` followed by `CREATE DATABASE customers`.

[NOTE]
====
The `IF NOT EXISTS` and `OR REPLACE` parts of these commands cannot be used together.
====

======

[[manage-databases-create-database-options]]
=== Options

The `CREATE DATABASE` command can have a map of options, e.g. `OPTIONS {key: 'value'}`.

[NOTE]
====
There are no available `OPTIONS` values for composite databases.
====

[options="header"]
|===

| Key | Value | Description

| `existingData`
| `use`
|
Controls how the system handles existing data on disk when creating the database.
Currently this is only supported with `existingDataSeedInstance` and must be set to `use` which indicates the existing data files should be used for the new database.

| `existingDataSeedInstance`
| ID of the cluster server
|
Defines which server is used for seeding the data of the created database.
The server ID can be found in the `serverId` column after running `SHOW SERVERS`.

| `seedURI`
| URI to a backup or a dump from an existing database.
|
Defines an identical seed from an external source which will be used to seed all servers.

| `seedConfig`
| comma separated list of configuration values.
|
Defines additional configuration specified by comma separated `name=value` pairs that might be required by certain seed providers.

| `seedCredentials`
| credentials
|
Defines credentials that needs to be passed into certain seed providers.

|===

[NOTE]
====
The `existingData`, `existingDataSeedInstance`, `seedURI`, `seedConfig` and `seedCredentials` options cannot be combined with the `OR REPLACE` part of this command.
More details about seeding options can be found clustering/databases.adoc#cluster-seed[here].
====


[[manage-databases-altering-databases]]
== Altering databases

Standard databases can be modified using the command `ALTER DATABASE`.

=== Access

By default, a database has read-write access mode on creation.
The database can be limited to read-only mode on creation using the configuration parameters `dbms.databases.default_to_read_only`, `dbms.databases.read_only`, and `dbms.database.writable`.
For details, see the section on xref::manage-databases/configuration.adoc#manage_database_parameters[configuration parameters] below.
A database that was created with read-write access mode can be changed to read-only.
To change it to read-only, you can use the `ALTER DATABASE` command with the sub-clause `SET ACCESS READ ONLY`.
Subsequently, the database access mode can be switched back to read-write using the sub-clause `SET ACCESS READ WRITE`.
Altering the database access mode is allowed at all times, whether a database is online or offline.

If conflicting modes are set by the `ALTER DATABASE` command and the configuration parameters, i.e. one says read-write and the other read-only, the database will be read-only and prevent write queries.

[NOTE]
====
Modifying access mode is only available to standard databases and not composite databases.
====


.+ALTER DATABASE+
======

.Query
[source, cypher]
----
ALTER DATABASE customers SET ACCESS READ ONLY
----

.Result
[source, result, role="noheader"]
----
System updates: 1
Rows: 0
----

======


.+SHOW DATABASES+
======

The database access mode can be seen in the `access` output column of the command `SHOW DATABASES`.

.Query
[source, cypher]
----
SHOW DATABASES yield name, access
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +name+ | +access+
| +"customers"+ | +"read-only"+
| +"movies"+ | +"read-write"+
| +"neo4j"+ | +"read-write"+
| +"system"+ | +"read-write"+
2+d|Rows: 7
|===

======


.+ALTER DATABASE+
======

`ALTER DATABASE` commands are optionally idempotent, with the default behavior to fail with an error if the database does not exist.
Appending `IF EXISTS` to the command ensures that no error is returned and nothing happens should the database not exist.

.Query
[source, cypher]
----
ALTER DATABASE nonExisting IF EXISTS
SET ACCESS READ WRITE
----

======

[[manage-databases-stopping-databases]]
== Stopping databases

Databases can be stopped using the command `STOP DATABASE`.


.+STOP DATABASE+
======

.Query
[source, cypher]
----
STOP DATABASE customers
----

.Result
[source, result, role="noheader"]
----
System updates: 1
Rows: 0
----

[NOTE]
====
Both standard databases and composite databases can be stopped using this command.
====

======


.+SHOW DATABASE+
======

The status of the stopped database can be seen using the command `SHOW DATABASE name`.

.Query
[source, cypher]
----
SHOW DATABASE customers YIELD name, requestedStatus, currentStatus
----

.Result
[role="queryresult",options="header,footer",cols="3*<m"]
|===
| +name+ | +requestedStatus+ | +currentStatus+
| +"customers"+ | +"offline"+ | +"offline"+
3+d|Rows: 1
|===

======

[[manage-databases]]
== Starting databases

Databases can be started using the command `START DATABASE`.

.+START DATABASE+
======

.Query
[source, cypher]
----
START DATABASE customers
----

.Result
[source, result, role="noheader"]
----
System updates: 1
Rows: 0
----

[NOTE]
====
Both standard databases and composite databases can be stopped using this command.
====

======


.+SHOW DATABASE+
======

The status of the started database can be seen using the command `SHOW DATABASE name`.

.Query
[source, cypher]
----
SHOW DATABASE customers YIELD name, requestedStatus, currentStatus
----

.Result
[role="queryresult",options="header,footer",cols="3*<m"]
|===
| +name+ | +requestedStatus+ | +currentStatus+
| +"customers"+ | +"online"+ | +"online"+
3+d|Rows: 1
|===

======

[[manage-databases-deleting-databases]]
== Deleting databases

Standard and composite databases can be deleted by using the command `DROP DATABASE`.


.+DROP DATABASE+
======

.Query
[source, cypher]
----
DROP DATABASE customers
----

.Result
[source, result, role="noheader"]
----
System updates: 1
Rows: 0
----

======


.+SHOW DATABASES+
======

When a database has been deleted, it will no longer show up in the listing provided by the command `SHOW DATABASES`.

.Query
[source, cypher]
----
SHOW DATABASES YIELD name
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===

| +name+
| +"movies"+
| +"neo4j"+
| +"system"+
13+d|Rows: 6

|===

======


.+DROP DATABASE+
======

This command is optionally idempotent, with the default behavior to fail with an error if the database does not exist.
Appending `IF EXISTS` to the command ensures that no error is returned and nothing happens should the database not exist.
It will always return an error, if there is an existing alias that targets the database. In that case, the alias needs to be dropped before dropping the database.

.Query
[source, cypher]
----
DROP DATABASE customers IF EXISTS
----

The `DROP DATABASE` command will remove a database entirely.

======



.+DROP DATABASE+
======

You can request that a dump of the store files is produced first, and stored in the path configured using the `dbms.directories.dumps.root` setting (by default `<neo4j-home>/data/dumps`).
This can be achieved by appending `DUMP DATA` to the command (or `DESTROY DATA` to explicitly request the default behavior).
These dumps are equivalent to those produced by `neo4j-admin dump` and can be similarly restored using `neo4j-admin load`.

Note that all database aliases must be dropped before dropping a database. 

////
[source, cypher, role=test-setup]
----
DROP ALIAS `films` FOR DATABASE;
DROP ALIAS `motion pictures` FOR DATABASE;
----
////

.Query
[source, cypher]
----
DROP DATABASE movies DUMP DATA
----

The options `IF EXISTS` and  `DUMP DATA`/ `DESTROY DATA` can also be combined.
An example could look like this:

.Query
[source, cypher]
----
DROP DATABASE customers IF EXISTS DUMP DATA
----

======

[[manage-databases-wait-options]]
== Wait options 

The_ `WAIT` _subclause was added as an option to the_ `ALTER DATABASE` _command in Neo4j 5.7._

Aside from `SHOW DATABASES`, all database management commands accept an optional `WAIT`/`NOWAIT` clause.
The `WAIT`/`NOWAIT` clause allows you to specify a time limit in which the command must complete and return.

The options are:

* `WAIT n SECONDS` - Return once completed or when the specified time limit of `n` seconds is up.
* `WAIT` - Return once completed or when the default time limit of 300 seconds is up.
* `NOWAIT` - Return immediately.

A command using a `WAIT` clause will automatically commit the current transaction when it executes successfully, as the command needs to run immediately for it to be possible to `WAIT` for it to complete.
Any subsequent commands executed will therefore be performed in a new transaction.
This is different to the usual transactional behavior, and for this reason it is recommended that these commands be run in their own transaction.
The default behavior is `NOWAIT`, so if no clause is specified the transaction will behave normally and the action is performed in the background post-commit.

[NOTE]
====
A command with a `WAIT` clause may be interrupted whilst it is waiting to complete.
In this event the command will continue to execute in the background and will not be aborted.
====


.+CREATE DATABASE+
======

.Query
[source, cypher]
----
CREATE DATABASE slow WAIT 5 SECONDS
----

.Result
[role="queryresult",options="header,footer",cols="4*<m"]
|===
| +address+ | +state+ | +message+ | +success+
| +"localhost:7687"+ | +"CaughtUp"+ | +"caught up"+ | +true+
4+d|Rows: 1
|===

The `success` column provides an aggregate status of whether or not the command is considered successful and thus every row will have the same value.
The intention of this column is to make it easy to determine, for example in a script, whether or not the command completed successfully without timing out.

A command with a `WAIT` clause may be interrupted whilst it is waiting to complete.
In this event the command will continue to execute in the background and will not be aborted.

======


[[manage-databases-parameters]]
== Configuration parameters

Configuration parameters are defined in the xref:configuration/neo4j-conf.adoc[neo4j.conf] file.

The following configuration parameters are applicable for managing databases:

[options="header", cols="40,60"]
|===
| Parameter name
| Description

| xref:configuration/configuration-settings.adoc#config_initial.dbms.default_database[`initial.dbms.default_database`]
a|
Name of the default database for the Neo4j instance.
The database is created if it does not exist when the instance starts.

*Default value:* `neo4j`

[NOTE]
====
In a clustered setup, the value of `initial.dbms.default_database` is only used to set the initial default database.
To change the default database at a later point, see xref:clustering/databases#cluster-default-database[Change the default database].
====

[NOTE]
====
Be aware that the automatically created _initial_ default database may have a different topology to the default configuration values.
See xref:clustering/clustering-advanced/default-database.adoc[Default database in a cluster] for more information.
====

| xref:configuration/configuration-settings.adoc#config_dbms.max_databases[`dbms.max_databases`]
a|
Maximum number of databases that can be used in a Neo4j single instance or cluster.
The number includes all the online and offline databases.
The value is an integer with a minimum value of 2.
label:enterprise-edition[]

*Default value:* `100`

[NOTE]
====
Once the limit has been reached, it is not possible to create any additional databases.
Similarly, if the limit is changed to a number lower than the total number of existing databases, no additional databases can be created.
====

| xref:configuration/configuration-settings.adoc#config_server.databases.default_to_read_only[`server.databases.default_to_read_only`]
a|
Default mode of all databases.
If this setting is set to `true` all existing and new databases will be in read only mode, and so will prevent write queries.

*Default value:* `false`

| xref:configuration/configuration-settings.adoc#config_server.databases.read_only[`server.databases.read_only`]
a|
List of database names for which to prevent write queries.
This set can contain also not yet existing databases, but not the `system` database.

[NOTE]
====
Regardless of settings of `server.databases.default_to_read_only`, `server.databases.read_only` and `dbms.databases.writable` the `system` database will never be read-only and will always accept write queries.
====

[NOTE]
====
Another way of preventing writes is to set the database access to read-only using the link:{neo4j-docs-base-uri}/cypher-manual/{page-version}/administration/databases#administration-databases-alter-database[ALTER DATABASE] command.
====


Example configuration:
[source, example, role="noheader"]
----
server.databases.read_only=["foo", "bar"]
----

| xref:configuration/dynamic-settings.adoc#config_dbms.databases.writable[`dbms.databases.writable`]
a|
List of database names for which to accept write queries.
This set can contain also not yet existing databases. +
The value of this setting is ignored if `server.databases.default_to_read_only` is set to `false`. +
If a database name is present in both sets, the database will be read-only and prevent write queries.

[[config-param-note-3]]
[TIP]
====
If most of your databases would read-only with a few exceptions, it can be easier to set `config_server.databases.default_to_read_only` to `true`, and then put the names of the non read-only databases into `dbms.databases.writeable`.
====

Example configuration:
[source, example, role="noheader"]
----
dbms.databases.writable=["foo", "bar"]
----
|===

[NOTE]
====
Although it is possible to achieve the same goal, i.e. set a database to read-only, both by using the Cypher command `ALTER DATABASE` and by using configuration parameters in `neo4j.conf`, it is important to understand the difference between the two.
`ALTER DATABASE foo SET ACCESS READ ONLY` effectively sets the database `foo` to read-only _across the entire DBMS_.

Using configuration parameters is more subtle and allows you to configure access on each instance separately, in case of a cluster for example.
If you use `server.databases.default_to_read_only` _all databases on that instance_ are set to read-only.

If both the Cypher command and the configuration parameters are used and they contain conflicting information, the database in question is set to read-only.
====
